script			: (top_level)*

top_level		: apply
				| section
				| declaration

apply			: '#' (('/' | '~' | '.') '/')? (IDENT '/')* IDENT
section			: IDENT '{' (top_level)* '}'

declaration		: variable_decl
				| function_decl
				| theorem_decl
				| conclusion_decl
				| question_decl

variable_decl	: '$' IDENT ':=' expression
function_decl	: '@' IDENT parameters annonation? ':=' expression
theorem_decl	: '!' IDENT parameters? ':=' expression ':=' expression
conclusion_decl	: '&' IDENT parameters? ('t' | 'f') ':=' theory
question_decl	: '?' IDENT? parameters? ':=' theory

parameters		: '(' (IDENT annonation? (',' IDENT annonation?)*)? ')'
annonation		: ':' TYPE

theory			: or
or				: xor ('|' xor)*
xor				: and ('!|' and)*
and				: binary ('&' binary)*
binary			: match | comparison | divisible | exists
match			: expression ('<=>' | '</=>') expression
comparison		: expression ('==' | '!=' | '>' | '>=' | '<' | '<=') expression
divisible		: expression '%' expression
exists			: expression '??'

expression		: equality
equality		: term (('=' | '/=') term)*
term			: factor (('-' | '+') factor)*
factor			: power (('/' | '*') power)*
power			: unary ('^' power)?
unary			: ('-') unary | primary
primary			: INTEGER | FLOAT
				| '(' expression ')'
				| variable | call

variable		: IDENT
call			: IDENT '(' (expression (',' expression)*)? ')'